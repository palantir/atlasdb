apply from: "../gradle/publish-jars.gradle"
apply from: "../gradle/shared.gradle"
apply from: "../gradle/feign-shading.gradle"

apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

schemas = [
    'com.palantir.atlasdb.schema.SweepSchema',
    'com.palantir.atlasdb.table.description.GenericTestSchema'
]

libsDirName = file('build/artifacts')

task myTestJar(type: Jar) {
    from sourceSets.test.output
    classifier = 'tests'
}

configurations {
    testArtifacts

    explicitShadow
    compile.extendsFrom(explicitShadow)
    shadow.extendsFrom(explicitShadow)
}

artifacts {
    testArtifacts myTestJar
}

dependencies {
  explicitShadow project(":atlasdb-commons")
  explicitShadow project(":atlasdb-api")
  explicitShadow project(":atlasdb-client-protobufs")
  explicitShadow (group: 'com.googlecode.json-simple', name: 'json-simple') {
    exclude group: 'junit'
  }
  explicitShadow group: "commons-lang", name: "commons-lang", version: libVersions.commons_lang
  explicitShadow group: "org.xerial.snappy", name: "snappy-java", version: libVersions.snappy
  explicitShadow group: "com.googlecode.protobuf-java-format", name: "protobuf-java-format", version: "1.2"
  explicitShadow group: "com.google.protobuf", name: "protobuf-java", version: "2.6.0"
  explicitShadow group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
  explicitShadow group: 'com.fasterxml.jackson.core', name: 'jackson-core'
  explicitShadow group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-guava'
  explicitShadow group: 'com.palantir.remoting2', name: 'tracing'
  explicitShadow group: 'com.palantir.tritium', name: 'tritium-lib'

  // Not needed at runtime in the shadow JAR, because we have shaded it away.
  compile group: 'com.netflix.feign', name: 'feign-jackson'
  compile (group: 'com.netflix.feign', name: 'feign-jaxrs') {
    exclude module: 'jsr311-api'
  }

  testCompile group: 'junit', name: 'junit'
  testCompile group: 'org.mockito', name: 'mockito-core'
  testCompile group: "org.jmock", name: "jmock", version: libVersions.jmock
  testCompile group: 'org.hamcrest', name: 'hamcrest-core'
  testCompile group: 'org.hamcrest', name: 'hamcrest-library'
  testCompile group: 'ch.qos.logback', name: 'logback-classic'
  testCompile group: 'org.assertj', name: 'assertj-core'
}
