apply plugin: 'com.github.johnrengelman.shadow'

apply from: "../gradle/shared.gradle"

configurations {
  explicitShadow
  shadow.extendsFrom(explicitShadow)
  compile.extendsFrom(explicitShadow)
}

dependencies {
  explicitShadow group: 'com.palantir.safe-logging', name: 'safe-logging'

  explicitShadow project(":atlasdb-client")
  explicitShadow project(":atlasdb-processors")
  explicitShadow project(":atlasdb-api")
  explicitShadow project(":commons-api")
  explicitShadow project(':timestamp-impl')
  explicitShadow project(":atlasdb-impl-shared")

  explicitShadow ('com.palantir.cassandra:cassandra-thrift:' + libVersions.palantir_cassandra_thrift) {
    exclude group: 'org.apache.httpcomponents'
  }

  explicitShadow ('com.datastax.cassandra:cassandra-driver-core:' + libVersions.cassandra_driver_core) {
    exclude(group: 'com.codahale.metrics', module: 'metrics-core')
  }
  explicitShadow group: 'com.google.guava', name: 'guava'

  explicitShadow 'org.apache.commons:commons-pool2'

  explicitShadow group: 'com.palantir.conjure.java.api', name: 'ssl-config'
  explicitShadow group: 'com.palantir.conjure.java.runtime', name: 'keystores'
  explicitShadow group: 'com.palantir.tracing', name: 'tracing'

  explicitShadow group: 'com.squareup.okio', name: 'okio'

  // does shadowing statically analysed annotations really buy us anything?
  explicitShadow group: 'com.google.code.findbugs', name: 'findbugs-annotations'
  explicitShadow group: 'com.google.code.findbugs', name: 'jsr305'

  explicitShadow group: 'org.jboss.marshalling', name: 'jboss-marshalling', version: '1.4.11.Final'

  testCompile project(path: ":atlasdb-client", configuration: "testArtifacts")
  testCompile project(":atlasdb-config")
  testCompile group: 'com.palantir.safe-logging', name: 'preconditions-assertj'
  testCompile group: 'org.mockito', name: 'mockito-core'
  testCompile group: 'org.assertj', name: 'assertj-core'
  testCompile group: 'org.awaitility', name: 'awaitility'
  testCompile group: 'uk.org.lidalia', name: 'slf4j-test', version: '1.1.0'
  testCompile(group: 'org.jmock', name: 'jmock') {
      exclude group: 'org.hamcrest'
  }

  annotationProcessor group: 'org.immutables', name: 'value'
  compileOnly 'org.immutables:value::annotations'
  testCompileOnly 'org.immutables:value::annotations'
  annotationProcessor 'com.google.auto.service:auto-service'
  compileOnly 'com.google.auto.service:auto-service'
  annotationProcessor project(":atlasdb-processors")
  compileOnly project(":atlasdb-processors")

  explicitShadow 'com.palantir.patches.sourceforge:trove3:' + libVersions.trove
}

shadowJar {
  mergeServiceFiles()
  classifier ''

  dependencies {
      include(dependency { false })
  }
}

jar.dependsOn shadowJar
jar.onlyIf { false }

apply plugin: 'com.palantir.sls-recommended-dependencies'

recommendedProductDependencies {
    productDependency {
        productGroup = 'com.palantir.rescue'
        productName = 'rescue'
        minimumVersion = '4.137.0'
        maximumVersion = '4.x.x'
    }

    productDependency {
        productGroup = 'com.palantir.cassandra'
        productName = 'sls-cassandra'
        minimumVersion = '3.31.0'
        maximumVersion = '6.x.x'
    }
}

// Kind of crap, but this would mangle the license in CPGOP which isn't allowed.
spotlessJavaCheck.enabled = false
