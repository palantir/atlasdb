plugins{
    id 'com.palantir.docker' version '0.5.2'
}

apply plugin: 'org.inferred.processors'
apply plugin: 'com.palantir.docker'

apply from: "../gradle/shared.gradle"

dependencies {
  testCompile project(":atlasdb-cassandra")
  testCompile project(":atlasdb-tests-shared")

  testCompile 'org.apache.cassandra:cassandra-all:' + libVersions.cassandra
  testCompile 'com.datastax.cassandra:cassandra-driver-core:' + libVersions.cassandra_driver_core

  testCompile 'org.mockito:mockito-core:1.9.5' 
  testCompile 'com.palantir.docker.compose:docker-compose-rule:0.5.3'
}

jacoco {
    toolVersion = "0.7.1.201405082137"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
    maxHeapSize = "1024m"
    include '**/*Suite.class'
    // We don't want to run ETEs not in the ETE containers.
    if (!Boolean.parseBoolean(System.getenv('ETE'))) {
        enabled = false
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

docker {
    name "com.palantir.docker/atlas-cassandra:2.2-v0.1"
    tags 'latest'
    dockerfile "${rootProject.projectDir}/docker-containers/cassandra/Dockerfile"
    files "${rootProject.projectDir}/docker-containers/cassandra/entrypoint.sh",
        "${rootProject.projectDir}/docker-containers/cassandra/cassandra.yaml",
        "${rootProject.projectDir}/docker-containers/cassandra/cassandra-env.sh"
    dependsOn jar
}
