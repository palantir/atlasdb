apply from: "../gradle/shared.gradle"

dependencies {
  compile(project(":atlasdb-cassandra"))
  testCompile project(path: ":atlasdb-tests-shared", configuration: 'test')
  testCompile 'org.apache.cassandra:cassandra-all:2.1.5'
  testCompile project(path: ':datastax-cassandra-driver-core', configuration: 'shadow')
}

jacoco {
    toolVersion = "0.7.1.201405082137"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }

    include '**/*Suite.class'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

// Add a classifier to properly pick up the datastax dependency
publishing {
    publications {
        local(MavenPublication) {
            pom.withXml {
                def node = asNode();
                node.depthFirst().each { n ->
                    if (n instanceof Node && n.text() == "datastax-cassandra-driver-core") {
                        n.parent().appendNode('classifier', 'all')
                        return;
                    }
                }
            }
        }
    }
}
