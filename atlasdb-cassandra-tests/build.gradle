apply from: "../gradle/shared.gradle"

// disable tests if building on Travis (Travis CI kills test processes due to load)
[test, check].each { it.onlyIf { !System.getenv().containsKey('CI') } }

dependencies {
  compile(project(":atlasdb-cassandra"))
  compile project(path: ":atlasdb-tests-shared")
  compile 'org.apache.cassandra:cassandra-all:2.1.5'
  compile 'com.palantir.shaded.com.datastax.cassandra:datastax-cassandra-driver-core:0.2.0'
}

jacoco {
    toolVersion = "0.7.1.201405082137"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }

    include '**/*Suite.class'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
        html.destination "${buildDir}/jacocoHtml"
    }
}

// Add a classifier to properly pick up the datastax dependency
publishing {
    publications {
        local(MavenPublication) {
            pom.withXml {
                def node = asNode();
                node.depthFirst().each { n ->
                    if (n instanceof Node && n.text() == "datastax-cassandra-driver-core") {
                        n.parent().appendNode('classifier', 'all')
                        return;
                    }
                }
            }
        }
    }
}
