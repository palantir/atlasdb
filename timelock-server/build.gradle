apply plugin: 'com.palantir.sls-java-service-distribution'
apply plugin: 'org.inferred.processors'
apply plugin: 'org.unbroken-dome.test-sets'

apply from: "../gradle/docker.gradle"
apply from: "../gradle/publish-dists.gradle"
apply from: "../gradle/shared.gradle"

testSets {
    integTest
}

jacocoTestReport {
    executionData test, integTest
}

check.dependsOn integTest

dependencies {
    compile project(":timestamp-impl")
    compile project(":lock-impl")
    compile project(":leader-election-impl")
    compile project(path: ":atlasdb-config", configuration: "shadow")

    compile group: 'com.github.rholder', name: 'guava-retrying'
    compile group: 'com.palantir.remoting1', name: 'jersey-servers'
    compile group: 'com.palantir.remoting1', name: 'ssl-config'
    compile group: 'com.palantir.remoting1', name: 'tracing'
    compile group: 'com.palantir.tritium', name: 'tritium-lib'
    compile group: 'io.atomix', name: 'atomix'
    compile group: 'io.atomix.catalyst', name: 'catalyst-netty'
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: libVersions.timelock_dropwizard
    compile group: 'io.dropwizard', name: 'dropwizard-http2', version: libVersions.timelock_dropwizard
    compile group: 'io.dropwizard', name: 'dropwizard-request-logging', version: libVersions.timelock_dropwizard
    compile group: 'io.dropwizard.modules', name: 'dropwizard-java8'
    compile group: 'org.eclipse.jetty', name: 'jetty-alpn-server'
    compile group: 'org.eclipse.jetty.http2', name: 'http2-common'

    runtime group: 'org.mortbay.jetty.alpn', name: 'jetty-alpn-agent', version: libVersions.jetty_alpn_agent

    processor group: 'org.immutables', name: 'value'

    testCompile project(":atlasdb-config")
    testCompile project(path: ":leader-election-impl", configuration: "testArtifacts")

    testCompile group: 'org.assertj', name: 'assertj-core'
    testCompile group: 'org.mockito', name: 'mockito-core'
    testCompile project(":atlasdb-tests-shared")

    integTestCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: libVersions.timelock_dropwizard
}

distribution {
    serviceName "timelock-server"
    mainClass 'com.palantir.atlasdb.timelock.TimeLockServerLauncher'
    args 'server', 'var/conf/timelock.yml'
    defaultJvmOpts "-Xms512m", "-Xmx512m", "-javaagent:service/lib/jetty-alpn-agent-${libVersions.jetty_alpn_agent}.jar"
}

dependencyRecommendations {
    strategy ConflictResolved
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            //specifying a fixed version for all libraries with 'io.dropwizard' group
            switch (details.requested.group) {
                case 'io.dropwizard':
                    details.useVersion libVersions.timelock_dropwizard
                    break
                case 'com.fasterxml.jackson.core':
                case 'com.fasterxml.jackson.datatype':
                    details.useVersion libVersions.timelock_jackson
                    break
                case 'org.eclipse.jetty':
                    details.useVersion libVersions.timelock_jetty
                    break
                case 'org.glassfish.jersey.core':
                    details.useVersion libVersions.timelock_jersey
                    break
                case 'com.google.guava':
                    details.useVersion libVersions.timelock_guava
                    break
            }
        }
        forcedModules = [
                'ch.qos.logback:logback-classic:1.1.7',
                'com.fasterxml.jackson.module:jackson-module-afterburner:2.6.7',
                'com.netflix.feign:feign-core:8.10.1',
                'io.atomix.catalyst:catalyst-transport:1.1.2',
                'org.objenesis:objenesis:2.3'
        ]
    }
}
