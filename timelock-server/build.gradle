apply plugin: 'org.unbroken-dome.test-sets'

apply from: "../gradle/shared.gradle"
apply from: "../gradle/tests.gradle"

testSets {
    libraries {
        testCommon {
            extendsFrom unitTest
        }
    }

    integTest {
        imports 'testCommon'
    }

    stressTest {
        imports 'testCommon'
    }

    suiteTest {
        imports 'testCommon'
    }
}

stressTest {
    filter {
        includeTestsMatching "com.palantir.atlasdb.timelock.stress.*"
    }
}

suiteTest {
    filter {
        includeTestsMatching "com.palantir.atlasdb.timelock.suite.*"
    }
}

check.dependsOn integTest
check.dependsOn suiteTest
check.dependsOn stressTest

dependencies {
    compile project(":dropwizard-metrics-hack")
    compile project(":timelock-agent")

    compile group: 'io.dropwizard', name: 'dropwizard-core'
    compile group: 'io.dropwizard', name: 'dropwizard-http2'
    compile group: 'io.dropwizard', name: 'dropwizard-request-logging'
    compile group: 'com.palantir.conjure.java.api', name: 'ssl-config'
    compile group: 'com.palantir.tritium', name: 'tritium-lib'

    annotationProcessor group: 'org.immutables', name: 'value'
    compileOnly 'org.immutables:value::annotations'
    testCompileOnly 'org.immutables:value::annotations'

    runtime project(":atlasdb-jdbc")
    runtime project(":atlasdb-hikari")
    runtime (project(":atlasdb-cassandra")) {
        exclude(module:'log4j-over-slf4j')
        exclude(module:'jcl-over-slf4j')
    }
    runtime project(":atlasdb-dbkvs")
    // TODO:   runtime "org.postgresql:postgresql:${postgresJdbcDriverVersion}"
    // TODO:   add runtime depencency on the dbkvs-oracle-driver internally

    testCompile (project(":atlasdb-tests-shared")) {
        exclude group: 'com.fasterxml.jackson.jaxrs'
    }

    testCompile group: 'com.palantir.conjure.java.runtime', name: 'client-config'
    testCompile project(":atlasdb-container-test-utils")
    testCompile project(":atlasdb-tests-shared")
    testCompile project(":atlasdb-dbkvs")
    testCompile (project(":atlasdb-cassandra")) {
        exclude(module:'log4j-over-slf4j')
        exclude(module:'jcl-over-slf4j')
    }
    testCompile project(":flake-rule")
    testCompile group: 'org.assertj', name: 'assertj-core'
    testCompile(group: 'org.jmock', name: 'jmock') {
        exclude group: 'org.hamcrest'
        exclude group: 'org.ow2.asm'
    }
    testCompile group: 'org.mockito', name: 'mockito-core'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock-standalone'
    testCompile group: 'com.squareup.okhttp3', name: 'okhttp'

    testCompile group: 'com.palantir.docker.compose', name: 'docker-compose-rule-core'
    testCompile group: 'com.palantir.docker.compose', name: 'docker-compose-rule-junit4'

    testCommonAnnotationProcessor group: 'org.immutables', name: 'value'
    testCommonCompileOnly 'org.immutables:value::annotations'

    testCommonImplementation group: 'com.github.peterwippermann.junit4', name: 'parameterized-suite'
    testCommonImplementation group: 'com.ea.agentloader', name: 'ea-agent-loader'
    testCommonImplementation group: 'io.dropwizard', name: 'dropwizard-testing'
    testCommonImplementation (project(":atlasdb-tests-shared")) {
        exclude group: 'com.fasterxml.jackson.jaxrs'
    }
    testCommonImplementation group: 'org.freemarker', name: 'freemarker'

    testCommonApi group: 'io.dropwizard', name: 'dropwizard-testing'
    testCommonApi group: 'com.github.peterwippermann.junit4', name: 'parameterized-suite'

    // TODO(fdesouza): Remove this once PDS-95791 is resolved.
    integTestImplementation project(":examples:profile-client")
}
