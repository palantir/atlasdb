import org.gradle.plugins.ide.idea.model.IdeaModel

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.4.4'
        classpath 'com.palantir.gradle.javadist:gradle-java-distribution:1.2.0'
        classpath 'com.palantir:gradle-baseline-java:0.7.1'
        classpath 'com.palantir:jacoco-coverage:0.4.0'
        classpath 'com.netflix.nebula:nebula-dependency-recommender:3.6.3'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:4.9.1'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'com.palantir.configuration-resolver' version '0.1.0'
    id 'com.palantir.git-version' version '0.5.2'
    id 'org.inferred.processors' version '1.2.4-rc2'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/palantir/releases/'
    }
}

apply from: 'gradle/versions.gradle'

group = 'com.palantir.atlasdb'
version = gitVersion()
description = 'Transactional distributed database layer'

task printLastVersion {
    doLast {
        def details = versionDetails()
        println details.lastTag
    }
}

apply plugin: 'com.palantir.jacoco-full-report'

jacoco {
    toolVersion = libVersions.jacoco
}

allprojects {
    apply plugin: 'nebula.dependency-recommender'

    dependencyRecommendations {
        strategy OverrideTransitives
        propertiesFile file: project.rootProject.file('versions.props')
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

subprojects {
    apply plugin: 'com.palantir.configuration-resolver'
    task allDeps(type: DependencyReportTask) {}
}

apply from: 'idea.gradle'

// Setup copyright notice as JavaDoc and no newline after it
project.afterEvaluate {
    def ideaRootModel = project.rootProject.extensions.findByType(IdeaModel)
    if (ideaRootModel) {
        ideaRootModel.project.ipr.withXml { provider ->
            def node = provider.asNode()
            def copyrightManager = node.component.find { it.'@name' == 'CopyrightManager' }
            copyrightManager.append(new XmlParser().parseText("""
                <LanguageOptions name="__TEMPLATE__">
                    <option name="addBlankAfter" value="false" />
                    <option name="separateBefore" value="true" />
                    <option name="lenBefore" value="2" />
                </LanguageOptions>
                """.stripIndent()))
        }
    }
}

def ideaSetModuleLevel(idea, targetCompatibility) {
    if (idea == null) return
    idea.module.jdkName = targetCompatibility
    idea.module.iml.withXml {
        it.asNode().component.find { it.@name == 'NewModuleRootManager' }.@LANGUAGE_LEVEL = ("JDK_" + targetCompatibility).replaceAll('\\.', '_')
    }
}

allprojects {
    // This allows tests that require an artefact to exist to
    // decide whether to call gradle themselves or not
    tasks.withType(Test) {
        systemProperty 'RUNNING_IN_GRADLE', 'true'
    }
}
