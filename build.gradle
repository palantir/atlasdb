import org.gradle.plugins.ide.idea.model.IdeaModel

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.7.5'
        classpath 'gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.30.0'
        classpath 'com.palantir.sls-packaging:gradle-sls-packaging:2.11.0'
        classpath 'com.netflix.nebula:nebula-dependency-recommender:5.2.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.4'
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.0'
    id 'com.palantir.configuration-resolver' version '0.2.0'
    id 'com.palantir.git-version' version '0.5.2'
    id 'org.inferred.processors' version '2.1.0'
    id 'org.unbroken-dome.test-sets' version '1.4.2'
    id 'com.google.protobuf' version '0.8.3'
}

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/palantir/releases/'
    }
}
apply plugin: 'com.palantir.baseline-config'
apply plugin: 'com.palantir.baseline-circleci'

dependencies {
    // Adds a dependency on the Baseline configuration files. Typically use
    // the same version as the plugin itself.
    baseline "com.palantir.baseline:gradle-baseline-java-config:0.30.0@zip"
}

apply from: 'gradle/versions.gradle'

group = 'com.palantir.atlasdb'
version = gitVersion()
description = 'Transactional distributed database layer'

task printLastVersion {
    doLast {
        def details = versionDetails()
        println details.lastTag
    }
}

allprojects {
    apply plugin: 'nebula.dependency-recommender'

    dependencyRecommendations {
        strategy OverrideTransitives
        propertiesFile file: project.rootProject.file('versions.props')
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }

        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.palantir.docker.compose' && details.requested.name == 'docker-compose-rule') {
                details.useTarget group: 'com.palantir.docker.compose', name: 'docker-compose-rule-junit4', version: dependencyRecommendations.getRecommendedVersion('com.palantir.docker.compose', 'docker-compose-rule-junit4')
            }
        }
    }
}

subprojects {
    apply plugin: 'com.palantir.configuration-resolver'
    task allDeps(type: DependencyReportTask) {}
}

apply from: 'gradle/idea.gradle'

// Setup copyright notice as a block comment, and no newline after it
project.afterEvaluate {
    def ideaRootModel = project.rootProject.extensions.findByType(IdeaModel)
    if (ideaRootModel) {
        ideaRootModel.project.ipr.withXml { provider ->
            def node = provider.asNode()
            def copyrightManager = node.component.find { it.'@name' == 'CopyrightManager' }
            copyrightManager.append(new XmlParser().parseText("""
                <LanguageOptions name="__TEMPLATE__">
                    <option name="addBlankAfter" value="false" />
                    <option name="separateBefore" value="true" />
                    <option name="lenBefore" value="2" />
                </LanguageOptions>
                """.stripIndent()))
            copyrightManager.append(new XmlParser().parseText("""
                <LanguageOptions name="JAVA">
                    <option name="fileTypeOverride" value="3" />
                </LanguageOptions>
                """.stripIndent()))
        }
    }
}

allprojects {
    // This allows tests that require an artefact to exist to
    // decide whether to call gradle themselves or not
    tasks.withType(Test) {
        systemProperty 'RUNNING_IN_GRADLE', 'true'
    }
}
