apply plugin: 'com.github.johnrengelman.shadow'
apply from: '../gradle/shared.gradle'

dependencies {
  api project(':atlasdb-service')
  implementation 'com.google.dagger:dagger'

  implementation 'com.google.errorprone:error_prone_annotations'
  implementation 'com.google.guava:guava'
  implementation 'com.palantir.refreshable:refreshable'
  implementation 'com.palantir.tritium:tritium-registry'
  implementation 'io.dropwizard.metrics:metrics-core'
  implementation 'javax.inject:javax.inject'
  implementation project(':atlasdb-api')
  implementation project(':atlasdb-client')
  implementation project(':atlasdb-config')
  implementation project(':atlasdb-impl-shared')
  implementation project(':commons-executors')
  implementation project(':lock-api')
  implementation project(':lock-api-objects')
  implementation project(':lock-impl')
  implementation project(':timestamp-api')

  annotationProcessor 'com.google.dagger:dagger-compiler'
  compileOnly 'com.google.dagger:dagger-compiler'
  annotationProcessor 'org.immutables:value'
  compileOnly 'org.immutables:value::annotations'

  testImplementation 'org.mockito:mockito-core'
  testImplementation 'org.assertj:assertj-core'
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testImplementation 'org.mockito:mockito-junit-jupiter'

  shadow project(':atlasdb-service')
}

shadowJar {
  mergeServiceFiles()
  archiveClassifier = ''

  relocate('dagger', 'com.palantir.atlasdb.shaded.dagger')

  dependencies {
    include dependency('com.google.dagger:dagger')
  }
}

jar.dependsOn shadowJar
jar.onlyIf { false }
