buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.palantir.gradle.javadist:gradle-java-distribution:1.0.1'
    }
}

apply from: "../gradle/shared.gradle"

apply plugin: 'com.palantir.java-distribution'
apply plugin: 'org.inferred.processors'


dependencies {
    compile project(':lock-impl')
    compile project(':leader-election-impl')
    compile project(':atlasdb-config')
    compile project(':atlasdb-hikari')
    compile project(':atlasdb-rocksdb')

    compile "io.dropwizard:dropwizard-core:" + libVersions.dropwizard
    compile 'com.google.dagger:dagger'

    compile "org.rocksdb:rocksdbjni:4.1.0"
    compile 'org.postgresql:postgresql:' + libVersions.postgresql

    compile group: 'org.apache.thrift', name: 'libthrift'

    processor 'org.immutables:value:' + libVersions.immutables
    processor('com.google.dagger:dagger-compiler') {
        // We need to explicitly exclude these so that intellij does not label them as provided
        if(gradle.startParameter.taskNames.contains('idea')) {
            exclude group: 'com.google.guava'
            exclude group: 'com.google.dagger', module: 'dagger'
        }
    }

    testCompile "io.dropwizard:dropwizard-testing:" + libVersions.dropwizard
}


task prepareForEteTests(type: Copy, dependsOn: 'distTar') {
    from distTar.outputs
    into 'build/docker/'

    rename { filename -> 'atlasdb-timelock-server-snapshot.tgz' }
}

distribution {
    serviceName 'atlasdb-timelock'
    mainClass 'com.palantir.atlasdb.timelock.server.TimeLockServer'
    args 'server', 'var/conf/timelock.yml'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
