import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

apply from: "../gradle/publish-jars.gradle"
apply plugin: 'org.inferred.processors'
apply plugin: 'com.github.johnrengelman.shadow'
apply from: "../gradle/feign-shading.gradle"

apply from: "../gradle/shared.gradle"

configurations {
    /*
     * The configurations below may be visualized as follows:
     *
     *   +-------+        +--------------+       +------+
     *   |compile+--+  +--+remotingShadow+---+   |shadow|
     *   +---+---+  |  |  +--------------+   |   +----+-+
     *       |      |  |                     |        |
     *       +------+--+---------------+     |        |
     *              |  |               |     |        |
     *         +----v--v-+          +--v-----v-----+  |
     *         |feignDeps|          |explicitShadow<--+
     *         +---------+          +--------------+
     *
     * where we have an arrow from A to B if configuration A extends from B (i.e. A gets all dependencies of B).
     *
     * remotingShadow includes compile dependencies, except for remoting. It also includes the ALPN agent for HTTP/2.
     * shadow includes compile dependencies, except for remoting and feign; it also includes the ALPN agent.
     *
     * Having shadow jar configurations extend from compile is not recommended
     * (see https://github.com/johnrengelman/shadow/issues/142).
     */

    remotingShadow

    explicitShadow
    compile.extendsFrom(explicitShadow)
    shadow.extendsFrom(explicitShadow)
    remotingShadow.extendsFrom(explicitShadow)

    feignDeps
    compile.extendsFrom(feignDeps)
    remotingShadow.extendsFrom(feignDeps)
}

dependencies {
    compile group: 'com.palantir.remoting2', name: 'error-handling'
    compile group: 'com.palantir.remoting2', name: 'jackson-support'

    explicitShadow project(':atlasdb-api')
    explicitShadow project(':atlasdb-impl-shared')
    explicitShadow project(':leader-election-impl')
    explicitShadow project(':lock-impl')

    explicitShadow group: 'javax.validation', name: 'validation-api'
    explicitShadow group: 'com.palantir.config.crypto', name: 'encrypted-config-value-module'
    explicitShadow group: 'com.palantir.tritium', name: 'tritium-lib'

    explicitShadow group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
    explicitShadow group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml'
    explicitShadow group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8'
    explicitShadow group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'
    explicitShadow group: 'io.dropwizard', name: 'dropwizard-jackson'
    explicitShadow group: 'com.google.code.findbugs', name: 'annotations'

    // Feign dependencies.
    // versions below 8.10.0 have a bug where POST requests must have a body
    feignDeps group: 'com.netflix.feign', name: 'feign-jackson'
    feignDeps (group: 'com.netflix.feign', name: 'feign-jaxrs') {
        exclude module: 'jsr311-api'
    }
    feignDeps group: 'com.netflix.feign', name: 'feign-okhttp'

    // This is added so that AtlasDB clients can specify the javaAgent as a JVM argument to load jars needed for HTTP/2
    // in the boot classpath
    shadow group: 'org.mortbay.jetty.alpn', name: 'jetty-alpn-agent', version: libVersions.jetty_alpn_agent
    remotingShadow group: 'org.mortbay.jetty.alpn', name: 'jetty-alpn-agent', version: libVersions.jetty_alpn_agent

    processor group: 'org.immutables', name: 'value'
    processor 'com.google.auto.service:auto-service:1.0-rc2'

    testCompile project(path: ":atlasdb-client", configuration: "testArtifacts")
    testCompile group: "org.jmock", name: "jmock", version: libVersions.jmock
    testCompile group: 'org.mockito', name: 'mockito-core'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock'
    testCompile group: 'org.assertj', name: 'assertj-core'
    
    // Needed for Jersey Response-based tests
    testCompile group: 'org.glassfish.jersey.core', name: 'jersey-common'
}

shadowJar {
    mergeServiceFiles()
    classifier ''

    relocate('com.palantir.remoting2.errors', 'com.palantir.atlasdb.shaded.com.palantir.remoting2.errors')
    relocate('com.palantir.remoting2.ext.jackson', 'com.palantir.atlasdb.shaded.com.palantir.remoting2.ext.jackson')

    dependencies {
        include(dependency(group: 'com.palantir.remoting2', name: 'error-handling'))
        include(dependency(group: 'com.palantir.remoting2', name: 'jackson-support'))
    }
}

// ONLY shade Remoting. For internal use.
task remotingShadowJar(type: ShadowJar) {
  mergeServiceFiles()
  classifier 'remotingShadow'
  from sourceSets.main.output
  configurations = [project.configurations.compile]

    relocate('com.palantir.remoting2.errors', 'com.palantir.atlasdb.shaded.com.palantir.remoting2.errors')
    relocate('com.palantir.remoting2.ext.jackson', 'com.palantir.atlasdb.shaded.com.palantir.remoting2.ext.jackson')

    dependencies {
        include(dependency(group: 'com.palantir.remoting2', name: 'error-handling'))
        include(dependency(group: 'com.palantir.remoting2', name: 'jackson-support'))
    }
}

artifacts {
    remotingShadow (tasks.remotingShadowJar.archivePath) {
        builtBy remotingShadowJar
    }
}

jar.dependsOn remotingShadowJar
jar.onlyIf { false }
