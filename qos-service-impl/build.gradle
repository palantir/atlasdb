apply plugin: 'org.inferred.processors'
apply plugin: 'org.unbroken-dome.test-sets'

apply from: "../gradle/publish-jars.gradle"
apply from: "../gradle/shared.gradle"
apply from: "../gradle/qos.gradle"

testSets {
    integTest
}

check.dependsOn integTest
dependencies {
    compile (project(":atlasdb-client")) {
		exclude group: 'com.squareup.okhttp3'
		exclude group: 'com.google.guava'
    }
    compile (project(":qos-service-api"));
    compile group: 'com.google.guava', name: 'guava'
    compile group: 'net.jcip', name: 'jcip-annotations'

    processor project(":atlasdb-processors")
    processor group: 'org.immutables', name: 'value'

    testCompile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml'
    testCompile group: 'org.assertj', name: 'assertj-core'
    testCompile group: 'org.mockito', name: 'mockito-core'
    testCompile(group: "org.jmock", name: "jmock", version: libVersions.jmock) {
        exclude group: 'org.hamcrest'
        exclude group: 'org.ow2.asm'
    }
    testCompile group: 'com.palantir.remoting-api', name: 'service-config'

    integTestCompile group: 'io.dropwizard', name: 'dropwizard-core'
    integTestCompile group: 'io.dropwizard', name: 'dropwizard-http2'
    integTestCompile group: 'io.dropwizard', name: 'dropwizard-request-logging'
    integTestCompile group: 'io.dropwizard', name: 'dropwizard-testing'
    integTestCompile group: 'com.palantir.remoting3', name: 'http2-agent'
    integTestCompile group: 'com.palantir.remoting3', name: 'jersey-servers'
    integTestCompile (group: 'com.palantir.remoting3', name: 'jaxrs-clients') {
        exclude (module:'jsr305')
    }
}

configurations.all {
    resolutionStrategy {
        force 'org.assertj:assertj-core:3.5.2'
        force 'org.hamcrest:hamcrest-core:1.3'
    }
}

def applyResolutionStrategy(config) {
    config.resolutionStrategy {
        force 'io.dropwizard.metrics:metrics-core:3.2.3'
        force 'org.hdrhistogram:HdrHistogram:2.1.10'
        force 'com.squareup.okhttp3:okhttp:3.8.1'
        dependencySubstitution {
            substitute module('io.dropwizard:dropwizard-jackson') with module(
                        'io.dropwizard:dropwizard-jackson:0.9.3')
        }
    }
}

applyResolutionStrategy(configurations.integTestCompile)
applyResolutionStrategy(configurations.integTestRuntime)
applyResolutionStrategy(configurations.integTestCompileClasspath)
applyResolutionStrategy(configurations.integTestRuntimeClasspath)
