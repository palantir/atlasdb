buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.palantir.gradle.javadist:gradle-java-distribution:0.5.2'
    }
}

apply from: "../gradle/shared.gradle"

apply plugin: 'com.palantir.java-distribution'
apply plugin: 'org.inferred.processors'


dependencies {
    compile project(':lock-impl')
    compile project(':leader-election-impl')
    compile project(':atlasdb-config')
    compile project(':atlasdb-hikari')
    compile project(':atlasdb-rocksdb')

    compile "io.dropwizard:dropwizard-core:" + libVersions.dropwizard
    compile 'com.google.dagger:dagger:2.0.2'

    runtime(project(':atlasdb-dbkvs')) {
        exclude(module:'HikariCP')
    }

    runtime(project(':atlasdb-cassandra')) {
        exclude(module:'log4j-over-slf4j')
        exclude(module:'jcl-over-slf4j')
        exclude(module:'json-simple')
        exclude(module:'metrics-core')
        exclude(module:'logback-core')
        exclude(module:'logback-classic')
        exclude(module:'joda-time')
        exclude(module:'snakeyaml')
        exclude(module:'hibernate-validator')
        exclude(module:'libthrift')
    }

    compile 'org.apache.thrift:libthrift:' + libVersions.libthrift

    processor 'org.immutables:value:2.0.21'
    processor('com.google.dagger:dagger-compiler:2.0.2') {
        // We need to explicitly exclude these so that intellij does not label them as provided
        if(gradle.startParameter.taskNames.contains('idea')) {
            exclude group: 'com.google.guava'
            exclude group: 'com.google.dagger', module: 'dagger'
        }
    }

    testCompile project(':atlasdb-ete-test-utils')

    testCompile "io.dropwizard:dropwizard-testing:" + libVersions.dropwizard
    testCompile 'com.palantir.docker.compose:docker-compose-rule:' + libVersions.dockerComposeRule
}

configurations.matching({ it.name in ['compile', 'runtime'] }).all {
    resolutionStrategy {
        force 'com.google.code.findbugs:jsr305:' + libVersions.jsr305
        force 'org.slf4j:slf4j-api:' + libVersions.slf4j
        force 'com.fasterxml.jackson.core:jackson-databind:' + libVersions.jackson
        force 'com.fasterxml.jackson.core:jackson-core:' + libVersions.jackson
        force 'org.apache.commons:commons-lang3:' + libVersions.commons_lang3
        force 'org.apache.thrift:libthrift:' + libVersions.libthrift
        force 'commons-codec:commons-codec:' + libVersions.commons_codec
        force 'org.hibernate:hibernate-validator:5.1.3.Final'
    }
}

task prepareForEteTests(type: Copy, dependsOn: ['distTar', ':atlasdb-ete-test-utils:buildCassandraImage']) {
    from distTar.outputs
    into 'build/docker/'

    rename { filename -> 'atlasdb-ete-snapshot.tgz' }
}

test.dependsOn 'prepareForEteTests'

distribution {
    serviceName 'atlasdb-ete'
    mainClass 'com.palantir.atlasdb.AtlasDbEteServer'
    args 'server', 'var/conf/atlasdb-ete.yml'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
