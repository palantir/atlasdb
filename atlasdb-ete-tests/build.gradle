apply from: "../gradle/shared.gradle"
apply from: "../gradle/tests.gradle"
apply from: "../gradle/non-client-dist.gradle"

apply plugin: 'com.palantir.sls-java-service-distribution'

schemas = ['com.palantir.atlasdb.blob.BlobSchema']

dependencies {
    compile project(':lock-impl')
    compile project(':leader-election-impl')
    compile project(':atlasdb-config')
    compile project(':atlasdb-ete-test-utils')
    compile project(':atlasdb-hikari')

    implementation project(':atlasdb-backup')
    implementation project(':atlasdb-cassandra')
    implementation project(':timelock-impl')
    implementation group: 'org.awaitility', name: 'awaitility'
    implementation group: 'com.palantir.tritium', name: 'tritium-lib'
    implementation group: 'io.dropwizard', name: 'dropwizard-core'

    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.google.errorprone:error_prone_annotations'
    implementation 'com.google.guava:guava'
    implementation 'com.google.protobuf:protobuf-java'
    implementation 'com.palantir.common:streams'
    implementation 'com.palantir.conjure.java.api:service-config'
    implementation 'com.palantir.conjure.java.runtime:conjure-java-jackson-serialization'
    implementation 'com.palantir.conjure.java.runtime:conjure-java-jersey-server'
    implementation 'com.palantir.safe-logging:preconditions'
    implementation 'com.palantir.safe-logging:safe-logging'
    implementation 'com.palantir.tritium:tritium-registry'
    implementation 'io.dropwizard.metrics:metrics-core'
    implementation 'io.dropwizard:dropwizard-configuration'
    implementation 'io.dropwizard:dropwizard-jersey'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api'
    implementation 'org.slf4j:slf4j-api'
    implementation project(':atlasdb-api')
    implementation project(':atlasdb-client')
    implementation project(':atlasdb-client-protobufs')
    implementation project(':atlasdb-commons')
    implementation project(':atlasdb-impl-shared')
    implementation project(':atlasdb-tests-shared')
    implementation project(':lock-api')
    implementation project(':lock-api-objects')
    implementation project(':timestamp-api')

    testImplementation 'com.google.errorprone:error_prone_annotations'
    testImplementation 'com.google.guava:guava'
    testImplementation 'com.palantir.conjure.java.api:ssl-config'
    testImplementation 'com.palantir.conjure.java.runtime:keystores'
    testImplementation 'com.palantir.docker.compose:docker-compose-rule-junit4'
    testImplementation 'com.palantir.docker.proxy:docker-proxy-rule-junit4'
    testImplementation 'com.palantir.safe-logging:preconditions'
    testImplementation 'com.palantir.safe-logging:safe-logging'
    testImplementation 'commons-io:commons-io'
    testImplementation 'joda-time:joda-time'
    testImplementation 'org.slf4j:slf4j-api'
    testImplementation project(':atlasdb-api')
    testImplementation project(':atlasdb-cassandra')
    testImplementation project(':atlasdb-cassandra').sourceSets.test.output
    testImplementation project(':atlasdb-client')
    testImplementation project(':atlasdb-remoting-api')
    testImplementation project(':atlasdb-tests-shared')
    testImplementation project(':lock-api-objects')
    testImplementation project(':timestamp-api')

    runtimeOnly project(':atlasdb-dbkvs')
    runtimeOnly project(':atlasdb-cassandra')

    compile (group: 'org.apache.thrift', name: 'libthrift') {
        exclude group: 'org.apache.httpcomponents'
    }

    annotationProcessor group: 'org.immutables', name: 'value'
    compileOnly 'org.immutables:value::annotations'
    annotationProcessor project(':atlasdb-processors')
    compileOnly project(':atlasdb-processors')

    testImplementation project(':atlasdb-container-test-utils')
    testImplementation project(':atlasdb-ete-test-utils')
    testImplementation project(':flake-rule')

    testAnnotationProcessor group: 'org.immutables', name: 'value'
    testCompileOnly 'org.immutables:value::annotations'

    testImplementation group: 'com.palantir.docker.compose', name: 'docker-compose-rule-core'
    testImplementation group: 'io.dropwizard', name: 'dropwizard-testing'
    testImplementation group: 'org.assertj', name: 'assertj-core'
}

task prepareForEteTests(type: Copy, dependsOn: 'distTar') {
    from distTar.outputs
    into 'build/docker/'

    rename { filename -> 'atlasdb-ete-snapshot.tgz' }
}

task longTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/MultiCassandraTestSuite.class'
}

task timeLockTest(type: Test) {
    dependsOn prepareForEteTests, ':timelock-server-distribution:dockerTag'
    include '**/*TimeLock*.class'
    exclude '**/*TimeLockMigration*.class'
}

task timeLockMigrationTest(type: Test) {
    dependsOn prepareForEteTests, ':timelock-server-distribution:dockerTag'
    include '**/*TimeLockMigration*.class'
}

task dbkvsTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/DbKvsTestSuite.class'
}

task oracleTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/OracleDbKvsEteTestSuite.class'
}

test {
    dependsOn longTest, prepareForEteTests, timeLockTest, dbkvsTest
    exclude '**/MultiCassandraTestSuite.class'

    exclude '**/*TimeLock*.class'
    // Technically covered by previous line, but explicitly avoiding in case of refactor
    exclude '**/*TimeLockMigration*.class'

    exclude '**/*EteTest.class'
    exclude '**/DbKvsTestSuite.class'
    exclude '**/OracleDbKvsEteTestSuite.class'
}

distribution {
    serviceName 'atlasdb-ete'
    mainClass 'com.palantir.atlasdb.AtlasDbEteServer'
    args 'server', 'var/conf/atlasdb-ete.yml'
    defaultJvmOpts '-Xmx768M'
}

sourceCompatibility = 11
targetCompatibility = 11
