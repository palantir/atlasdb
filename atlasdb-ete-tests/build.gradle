apply from: "../gradle/shared.gradle"
apply from: "../gradle/tests.gradle"
apply from: "../gradle/non-client-dist.gradle"

apply plugin: 'com.palantir.sls-java-service-distribution'

schemas = ['com.palantir.atlasdb.blob.BlobSchema']

dependencies {
    compile project(":dropwizard-metrics-hack")
    compile project(':lock-impl')
    compile project(':leader-election-impl')
    compile project(':atlasdb-config')
    compile project(':atlasdb-ete-test-utils')
    compile project(':atlasdb-hikari')

    compile group: 'org.awaitility', name: 'awaitility'
    compile group: 'com.palantir.tritium', name: 'tritium-lib'
    compile group: 'io.dropwizard', name: 'dropwizard-core'

    runtime project(':atlasdb-dbkvs')
    runtime project(':atlasdb-cassandra')

    compile (group: 'org.apache.thrift', name: 'libthrift') {
        exclude group: 'org.apache.httpcomponents'
    }

    annotationProcessor group: 'org.immutables', name: 'value'
    compileOnly 'org.immutables:value::annotations'
    annotationProcessor project(':atlasdb-processors')
    compileOnly project(':atlasdb-processors')

    testCompile project(':atlasdb-container-test-utils')
    testCompile project(':atlasdb-ete-test-utils')
    testCompile project(':flake-rule')

    testAnnotationProcessor group: 'org.immutables', name: 'value'
    testCompileOnly 'org.immutables:value::annotations'

    testCompile group: 'com.palantir.docker.compose', name: 'docker-compose-rule-core'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing'
    testCompile group: 'org.assertj', name: 'assertj-core'
}

task prepareForEteTests(type: Copy, dependsOn: 'distTar') {
    from distTar.outputs
    into 'build/docker/'

    rename { filename -> 'atlasdb-ete-snapshot.tgz' }
}

task longTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/MultiCassandraTestSuite.class'
}

task timeLockTest(type: Test) {
    dependsOn prepareForEteTests, ':timelock-server-distribution:dockerTag'
    include '**/*TimeLock*.class'
    exclude '**/*TimeLockMigration*.class'
}

task timeLockMigrationTest(type: Test) {
    dependsOn prepareForEteTests, ':timelock-server-distribution:dockerTag'
    include '**/*TimeLockMigration*.class'
}

task dbkvsTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/DbKvsTestSuite.class'
}

task oracleTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/OracleDbKvsEteTestSuite.class'
}

test {
    dependsOn longTest, prepareForEteTests, timeLockTest, dbkvsTest
    exclude '**/MultiCassandraTestSuite.class'

    exclude '**/*TimeLock*.class'
    // Technically covered by previous line, but explicitly avoiding in case of refactor
    exclude '**/*TimeLockMigration*.class'

    exclude '**/*EteTest.class'
    exclude '**/DbKvsTestSuite.class'
    exclude '**/OracleDbKvsEteTestSuite.class'
}

distribution {
    serviceName 'atlasdb-ete'
    mainClass 'com.palantir.atlasdb.AtlasDbEteServer'
    args 'server', 'var/conf/atlasdb-ete.yml'
    defaultJvmOpts '-Xmx768M'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
