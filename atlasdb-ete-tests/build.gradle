apply from: "../gradle/publish-jars.gradle"
apply from: "../gradle/shared.gradle"

apply plugin: 'com.palantir.sls-java-service-distribution'

schemas = [
        'com.palantir.atlasdb.cas.CheckAndSetSchema',
        'com.palantir.atlasdb.blob.BlobSchema'
]

dependencies {
    compile project(':lock-impl')
    compile project(':leader-election-impl')
    compile project(':atlasdb-config')
    compile project(':atlasdb-ete-test-utils')
    compile project(':atlasdb-hikari')

    compile group: 'org.awaitility', name: 'awaitility'
    compile group: 'com.palantir.tritium', name: 'tritium-lib'
    compile group: 'io.dropwizard', name: 'dropwizard-core'

    runtime project(':atlasdb-dbkvs')

    runtime project(':atlasdb-cassandra')

    compile group: 'org.apache.thrift', name: 'libthrift'

    processor group: 'org.immutables', name: 'value'

    testCompile project(':atlasdb-container-test-utils')
    testCompile project(':atlasdb-ete-test-utils')
    testCompile project(':flake-rule')

    testCompile group: 'com.palantir.docker.compose', name: 'docker-compose-rule-core'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing'
    testCompile group: 'org.assertj', name: 'assertj-core'
}

task prepareForEteTests(type: Copy, dependsOn: 'distTar') {
    from distTar.outputs
    into 'build/docker/'

    rename { filename -> 'atlasdb-ete-snapshot.tgz' }
}

task longTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/MultiCassandraTestSuite.class'
}

task timeLockTest(type: Test) {
    dependsOn prepareForEteTests, ':timelock-server-distribution:dockerTag'
    include '**/*TimeLock*.class'

    // TODO (jkong): Re-enable migration tests once a new story for handling volumes-in-circle-2 is available.
    // See: https://discuss.circleci.com/t/mount-project-folder-using-docker-compose-and-circle-ci/22923
    exclude '**/*TimeLockMigration*.class'
}

task startupIndependenceTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/StartupIndependence*Test.class'
}

test {
    dependsOn longTest, prepareForEteTests, timeLockTest, startupIndependenceTest
    exclude '**/MultiCassandraTestSuite.class'
    exclude '**/*TimeLock*.class'
    exclude '**/*EteTest.class'
}

distribution {
    serviceName 'atlasdb-ete'
    mainClass 'com.palantir.atlasdb.AtlasDbEteServer'
    args 'server', 'var/conf/atlasdb-ete.yml'
    defaultJvmOpts '-Xmx384M'
    ignoredProductDependency('com.palantir.rescue', 'rescue')
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
