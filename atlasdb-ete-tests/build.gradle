apply from: "../gradle/publish-jars.gradle"
apply from: "../gradle/shared.gradle"

apply plugin: 'com.palantir.java-distribution'
apply plugin: 'org.inferred.processors'

dependencies {
    compile(project(':lock-impl')) {
        // dropwizard-core gives us a different version of these libs
        exclude group: 'com.codahale.metrics'
    }
    compile(project(':leader-election-impl')) {
        // dropwizard-core gives us a different version of these libs
        exclude group: 'com.codahale.metrics'
    }
    compile(project(':atlasdb-config')) {
        // dropwizard-core gives us a different version of these libs
        exclude group: 'com.codahale.metrics'
    }
    compile project(':atlasdb-dropwizard-bundle')
    compile(project(':atlasdb-hikari')) {
        // dropwizard-core gives us a different version of these libs
        exclude group: 'com.codahale.metrics'
    }
    compile(project(':atlasdb-rocksdb')) {
        // dropwizard-core gives us a different version of these libs
        exclude group: 'com.codahale.metrics'
    }

    compile group: 'io.dropwizard', name: 'dropwizard-core'

    runtime project(':atlasdb-dbkvs')

    runtime project(':atlasdb-cassandra')

    compile group: 'org.apache.thrift', name: 'libthrift'

    processor group: 'org.immutables', name: 'value'

    testCompile project(':atlasdb-container-test-utils')
    testCompile project(':atlasdb-docker-test-utils')
    testCompile project(':atlasdb-ete-test-utils')

    testCompile group: 'io.dropwizard', name: 'dropwizard-testing'
    testCompile 'com.palantir.docker.compose:docker-compose-rule:' + libVersions.dockerComposeRule
}

task prepareForEteTests(type: Copy, dependsOn: 'distTar') {
    from distTar.outputs
    into 'build/docker/'

    rename { filename -> 'atlasdb-ete-snapshot.tgz' }
}

task longTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/MultiCassandraTestSuite.class'
}

test {
    dependsOn longTest, prepareForEteTests
    exclude '**/MultiCassandraTestSuite.class'
    exclude '**/*EteTest.class'
}

distribution {
    serviceName 'atlasdb-ete'
    mainClass 'com.palantir.atlasdb.AtlasDbEteServer'
    args 'server', 'var/conf/atlasdb-ete.yml'
    defaultJvmOpts '-Xmx384M'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
