buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.palantir.gradle.javadist:gradle-java-distribution:1.0.1'
    }
}

apply from: "../gradle/shared.gradle"

apply plugin: 'com.palantir.java-distribution'
apply plugin: 'org.inferred.processors'

dependencies {
    compile project(':lock-impl')
    compile project(':leader-election-impl')
    compile project(':atlasdb-config')
    compile project(':atlasdb-dropwizard-bundle')
    compile project(':atlasdb-hikari')
    compile project(':atlasdb-rocksdb')

    compile "io.dropwizard:dropwizard-core:" + libVersions.dropwizard

    runtime project(':atlasdb-dbkvs')

    runtime project(':atlasdb-cassandra')

    compile group: 'org.apache.thrift', name: 'libthrift'

    processor group: 'org.immutables', name: 'value'

    testCompile project(':atlasdb-ete-test-utils')
    testCompile project(':atlasdb-docker-test-utils')

    testCompile "io.dropwizard:dropwizard-testing:" + libVersions.dropwizard
    testCompile 'com.palantir.docker.compose:docker-compose-rule:' + libVersions.dockerComposeRule
}

task prepareForEteTests(type: Copy, dependsOn: 'distTar') {
    from distTar.outputs
    into 'build/docker/'

    rename { filename -> 'atlasdb-ete-snapshot.tgz' }
}

task longTest(type: Test) {
    dependsOn prepareForEteTests
    include '**/MultiCassandraTestSuite.class'
}

test {
    dependsOn longTest, prepareForEteTests
    exclude '**/MultiCassandraTestSuite.class'
    exclude '**/*EteTest.class'
}

distribution {
    serviceName 'atlasdb-ete'
    mainClass 'com.palantir.atlasdb.AtlasDbEteServer'
    args 'server', 'var/conf/atlasdb-ete.yml'
    defaultJvmOpts '-Xmx384M'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
