apply plugin: 'com.palantir.java-distribution'
apply plugin: 'org.inferred.processors'

apply from: "${rootProject.projectDir}/gradle/shared.gradle"

dependencies {
    compile project(':atlasdb-config')

    runtime project(':atlasdb-cassandra')
    runtime project(':atlasdb-dbkvs')

    testCompile project(':atlasdb-docker-test-utils')
    testCompile project(':atlasdb-ete-test-utils')

    compile group: 'io.dropwizard', name: 'dropwizard-core', version: libVersions.dropwizard

    processor group: 'org.immutables', name: 'value', version: libVersions.immutables

    testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.6.5'
    testCompile group: 'com.palantir.docker.compose', name: 'docker-compose-rule', version: libVersions.dockerComposeRule
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: libVersions.dropwizard
}

test {
    maxHeapSize = "512m"
    dependsOn ':atlasdb-ete-test-utils:buildCassandraImage'
}

configurations.matching({ it.name in ['compile', 'runtime'] }).all {
    resolutionStrategy {
        force group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.3'
        force group: 'ch.qos.logback', name: 'logback-core', version: '1.1.3'
        force group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
        force group: 'commons-codec', name: 'commons-codec', version: libVersions.commons_codec
        force group: 'io.dropwizard.metrics', name: 'metrics-core', version: '3.1.1'
        force group: 'joda-time', name: 'joda-time', version: libVersions.joda_time
        force group: 'org.apache.commons', name: 'commons-lang3', version: libVersions.commons_lang3
        force group: 'org.apache.thrift', name: 'libthrift', version: libVersions.libthrift
        force group: 'org.hibernate', name: 'hibernate-validator', version: '5.1.3.Final'
        force group: 'org.slf4j', name: 'jcl-over-slf4j', version: libVersions.slf4j
        force group: 'org.slf4j', name: 'log4j-over-slf4j', version: libVersions.slf4j
        force group: 'org.yaml', name: 'snakeyaml', version: '1.12'
    }
}

distribution {
    serviceName 'atlasdb-server'
    mainClass 'com.palantir.atlasdb.server.AtlasDbServer'
    args 'server', 'var/conf/atlasdb-server.yml'

    excludeFromVar 'data'
}
