apply plugin: 'com.palantir.baseline-checkstyle'

// ./gradlew compileJava compileTestJava -PenableErrorProne=true
if (enableErrorProne.toBoolean()) {
    apply plugin: 'org.inferred.processors'
    // installs the "processor" configuration needed for baseline-error-prone
    apply plugin: 'com.palantir.baseline-error-prone'

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            compileOnly 'com.google.code.findbugs:jsr305'
        }

        tasks.withType(JavaCompile).configureEach {
            options.errorprone.errorproneArgs += [
                '-XepDisableWarningsInGeneratedCode',
                '-XepExcludedPaths:.*(generated|Immutable).*',
            ]
        }

        compileJava {
            options.errorprone.errorproneArgs += [
                '-Xep:PreconditionsConstantMessage:OFF',
                '-Xep:ValidateConstantMessage:OFF',
                // I'd like to change this, but preserving log message format for now
                '-Xep:CatchBlockLogException:OFF',
            ]
        }

        compileTestJava {
            // CheckReturnValue can be problematic for tests asserting that various Immutable objects may or may not
            // be created.
            options.errorprone.errorproneArgs += [
                '-Xep:CheckReturnValue:OFF',
                // ReturnValueIgnored can be problematic for tests asserting a call to some method throws an exception.
                '-Xep:ReturnValueIgnored:OFF',
            ]
        }
    })
}

apply plugin: 'com.palantir.baseline-eclipse'
apply plugin: 'com.palantir.baseline-idea'
