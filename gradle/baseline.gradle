apply plugin: 'com.palantir.baseline-checkstyle'

// ./gradlew compileJava compileTestJava -PenableErrorProne=true
if (enableErrorProne.toBoolean()) {
    apply plugin: 'org.inferred.processors'
    // installs the "processor" configuration needed for baseline-error-prone
    apply plugin: 'com.palantir.baseline-error-prone'

    configurations.errorprone {
        resolutionStrategy { force('com.google.guava:guava:21.0') }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-XepDisableWarningsInGeneratedCode']
    }

    compileJava {
        options.compilerArgs += ['-Xep:PreconditionsConstantMessage:OFF']
        options.compilerArgs += ['-Xep:ValidateConstantMessage:OFF']
    }

    compileTestJava {
        // CheckReturnValue can be problematic for tests asserting that various Immutable objects may or may not
        // be created.
        options.compilerArgs += ['-Xep:CheckReturnValue:OFF']
    }
}

apply plugin: 'com.palantir.baseline-eclipse'
apply plugin: 'com.palantir.baseline-idea'
apply plugin: 'com.palantir.baseline-circleci'
