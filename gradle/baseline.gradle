List<String> blacklistedBaselineProjects = [
        'atlasdb-client',
        'atlasdb-client-protobufs',
        'atlasdb-commons',
        'atlasdb-console',
        'atlasdb-dbkvs-hikari',
        'atlasdb-jdbc',
        'atlasdb-jdbc-tests',
        'atlasdb-service-server',
        'cassandra-partitioner',
        'commons-db',
        'commons-executors',
        'leader-election-impl',
        'lock-impl',
        'profile-client-protobufs',
        'timestamp-impl']

if (!blacklistedBaselineProjects.contains(project.name)) {
    apply plugin: 'com.palantir.baseline-checkstyle'
}
// ./gradlew compileJava compileTestJava -PenableErrorProne=true
if (enableErrorProne.toBoolean()) {
    apply plugin: 'org.inferred.processors'
    // installs the "processor" configuration needed for baseline-error-prone
    apply plugin: 'com.palantir.baseline-error-prone'

    configurations.errorprone {
        resolutionStrategy { force('com.google.guava:guava:21.0') }
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-XepDisableWarningsInGeneratedCode']
    }

    compileJava {
        options.compilerArgs += ['-Xep:PreconditionsConstantMessage:OFF']
        options.compilerArgs += ['-Xep:ValidateConstantMessage:OFF']
    }
}

apply plugin: 'com.palantir.baseline-eclipse'
apply plugin: 'com.palantir.baseline-idea'
