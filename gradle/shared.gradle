apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'com.palantir.jacoco-coverage'
apply plugin: 'com.github.hierynomus.license'

apply from: "${rootDir}/gradle/versions.gradle"
apply from: "${rootDir}/gradle/baseline.gradle"

sourceCompatibility = 1.8

version = rootProject.version
group = rootProject.group

repositories {
    mavenCentral()
    maven {
        url 'http://dl.bintray.com/palantir/releases/'
    }
    maven {
        url 'https://dl.bintray.com/marshallpierce/maven/'
    }
}

checkstyle {
    toolVersion = libVersions.checkstyle
    configFile rootProject.file('config/checkstyle/checkstyle.xml')
}

jacoco {
    toolVersion = libVersions.jacoco
}

jar {
    manifest {
        attributes(
                "Implementation-Title"   : project.name,
                "Implementation-Version" : project.version,
                "Implementation-Vendor"  : "Palantir Technologies, Inc.")
    }
}

libsDirName = file('build/artifacts')

dependencies {
    // Dont add Java8 compile/runtime dependencies here as commons-executors needs to be compatible with Java6 clients
    compile group: 'com.google.code.findbugs', name: 'annotations'
    checkstyle group: 'com.puppycrawl.tools', name: 'checkstyle', version: libVersions.checkstyle
    testCompile group: 'junit', name: 'junit'
}

apply from: rootProject.file('gradle/javadoc.gradle'), to: javadoc

license {
    header rootProject.file('config/HEADER')
    skipExistingHeaders true

    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    include '**/*.java'
    exclude '**/generated/**/*.java'
    exclude '**/RateLimiter.java'
    exclude '**/SmoothRateLimiter.java'
}

ext {
    schemas = []
}

task generateSchemas {
    description = 'Re-generate code from AtlasDB schemas.'
    dependsOn classes, testClasses
}

afterEvaluate {
    schemas.each { schema ->
        generateSchemas {
            doLast {
                logger.info "Processing schemas for ${schema}"
                javaexec {
                    main schema
                    classpath sourceSets.test.runtimeClasspath
                }
            }
        }
    }
}

tasks.withType(Test) {
    if (System.env.CIRCLE_TEST_REPORTS) {
        reports.junitXml.destination = new File(System.env.CIRCLE_TEST_REPORTS, it.getName())
    }

    enableAssertions = true

    testLogging {
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        events "started", "passed", "skipped", "failed"
    }
}

task allCompileDeps(type: DependencyReportTask) {
    configurations = [project.configurations.getByName("compile")]
}

apply plugin: 'nebula.dependency-lock'
dependencyLock {
    includeTransitives = true
    lockFile = 'versions.lock'
    configurationNames = ['runtime', 'compileClasspath']
}

task verifyDependencyLocksAreCurrent {
    doLast {
        def expectedDependencies = tasks.saveLock.getOutputLock()
        def actualDependencies = tasks.saveLock.getGeneratedLock()

        def digester = java.security.MessageDigest.getInstance('SHA')
        logger.info("Verifying integrity of dependency locks: {} vs {}", expectedDependencies, actualDependencies)
        if (digester.digest(expectedDependencies.bytes) != digester.digest(actualDependencies.bytes)) {
            throw new Exception("The dependencies of project " + project.name + " do not match the expected "
                    + "dependencies recorded in " + expectedDependencies + ". "
                    + "Run `./gradlew generateLock saveLock` and commit the updated version.lock files")
        }
    }
}

check.dependsOn generateLock, verifyDependencyLocksAreCurrent

ext.atlasdb_shaded = 'com.palantir.atlasdb.shaded.'
