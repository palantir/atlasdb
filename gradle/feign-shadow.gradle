apply plugin: 'com.github.johnrengelman.shadow'

configurations {
    explicitShadow
    shadow.extendsFrom(explicitShadow)
    compile.extendsFrom(explicitShadow)
}

dependencies {
    // Keep in sync with the dependencies excluded from shadowJar.
    explicitShadow group: 'com.netflix.feign', name: 'feign-jackson'
    explicitShadow(group: 'com.netflix.feign', name: 'feign-jaxrs') {
        exclude module: 'jsr311-api'
    }
    explicitShadow group: 'com.netflix.feign', name: 'feign-okhttp'
}

shadowJar {
    mergeServiceFiles()
    relocate('feign', 'atlasdb.shaded.feign')
    classifier = ''
    dependencies {
        include (dependency(group: 'com.netflix.feign', name: 'feign-jackson'))
        include (dependency(group: 'com.netflix.feign', name: 'feign-jaxrs'))
        include (dependency(group: 'com.netflix.feign', name: 'feign-okhttp'))
        exclude (dependency(group: 'javax.ws.rs', name: 'jsr311-api'))
    }
}

jar.dependsOn shadowJar
jar.onlyIf { false }