apply from: "${rootProject.projectDir}/gradle/shared.gradle"

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.avast.gradle:docker-compose-gradle-plugin:0.3.5'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'docker-compose'

task ensureDateUtilPresent(type: Exec) {
    commandLine 'sh', 'scripts/ensure-date-util-present.sh'
}

task runUnitTestsForPrintLogsInChronologicalOrderScript(type: Exec) {
    dependsOn ensureDateUtilPresent
    commandLine 'python', 'scripts/print_logs_in_chronological_order.py', '--tests-only'
}

check.dependsOn runUnitTestsForPrintLogsInChronologicalOrderScript

dependencies {
    api project(':atlasdb-api')
    api project(':atlasdb-commons')
    api project(':atlasdb-config')
    api project(':atlasdb-impl-shared')
    api project(':atlasdb-remoting-api')
    api project(':commons-executors')
    api project(':lock-api')
    api project(':lock-api-objects')
    api project(':timelock-api:timelock-api-objects')
    api project(':timestamp-api')

    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.google.errorprone:error_prone_annotations'
    implementation 'com.google.guava:guava'
    implementation 'com.palantir.common:streams'
    implementation 'com.palantir.conjure.java.api:service-config'
    implementation 'com.palantir.conjure.java.api:ssl-config'
    implementation 'com.palantir.dialogue:dialogue-clients'
    implementation 'com.palantir.refreshable:refreshable'
    implementation 'com.palantir.safe-logging:preconditions'
    implementation 'com.palantir.safe-logging:safe-logging'
    implementation 'org.clojure:clojure'
    implementation 'org.slf4j:slf4j-api'

    testImplementation 'com.palantir.common:streams'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation project(':lock-api')
    testImplementation project(':lock-api-objects')

    annotationProcessor 'org.immutables:value'
    compileOnly 'org.immutables:value::annotations'
}

shadowJar {
    dependencies {
        exclude(dependency(group: 'org.clojure', name: 'clojure'))
    }
}

task copyShadowJar(type: Copy, dependsOn: 'shadowJar') {
    from tasks.getByPath('shadowJar').outputs
    into 'resources/atlasdb/'
    rename { filename -> 'atlasdb-jepsen-tests-all.jar' }
}

task copyTimelockServer(type: Copy, dependsOn: ':timelock-server-distribution:distTar') {
    from tasks.getByPath(':timelock-server-distribution:distTar').outputs
    into 'resources/atlasdb/'
    rename { filename -> 'timelock-server.tgz' }
}

task jepsenTest(type: Exec, dependsOn: ['copyShadowJar', 'copyTimelockServer']) {
    environment = project.dockerCompose.environment
    commandLine project.dockerCompose.composeCommand('run', '--rm', 'jepsen')
}

check.dependsOn jepsenTest
