apply from: "${rootProject.projectDir}/gradle/shared.gradle"

buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }
    dependencies {
        classpath 'com.avast.gradle:gradle-docker-compose-plugin:0.16.9'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'docker-compose'

task ensureDateUtilPresent(type: Exec) {
    commandLine 'sh', 'scripts/ensure-date-util-present.sh'
}

task runUnitTestsForPrintLogsInChronologicalOrderScript(type: Exec) {
    dependsOn ensureDateUtilPresent
    commandLine 'python', 'scripts/print_logs_in_chronological_order.py', '--tests-only'
}

check.dependsOn runUnitTestsForPrintLogsInChronologicalOrderScript

versionsLock {
    testProject()
}

dependencies {
    implementation project(':atlasdb-config')

    implementation 'com.fasterxml.jackson.core:jackson-core'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.google.guava:guava'
    implementation 'org.clojure:clojure'

    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'com.google.errorprone:error_prone_annotations'
    implementation 'com.palantir.common:streams'
    implementation 'com.palantir.conjure.java.api:service-config'
    implementation 'com.palantir.conjure.java.api:ssl-config'
    implementation 'com.palantir.dialogue:dialogue-clients'
    implementation 'com.palantir.refreshable:refreshable'
    implementation 'com.palantir.safe-logging:preconditions'
    implementation 'com.palantir.safe-logging:safe-logging'
    implementation 'com.palantir.tritium:tritium-registry'
    implementation project(':atlasdb-api')
    implementation project(':atlasdb-commons')
    implementation project(':atlasdb-impl-shared')
    implementation project(':atlasdb-remoting-api')
    implementation project(':commons-executors')
    implementation project(':lock-api')
    implementation project(':lock-api-objects')
    implementation project(':timelock-api')
    implementation project(':timelock-api:timelock-api-jersey')
    implementation project(':timestamp-api')

    testImplementation 'com.palantir.common:streams'
    testImplementation project(':lock-api')
    testImplementation project(':lock-api-objects')

    annotationProcessor 'org.immutables:value'
    compileOnly 'org.immutables:value::annotations'

    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

shadowJar {
    dependencies {
        exclude(dependency('org.clojure:clojure'))
    }
}

task copyShadowJar(type: Copy, dependsOn: 'shadowJar') {
    from tasks.getByPath('shadowJar').outputs
    into 'resources/atlasdb/'
    rename { filename -> 'atlasdb-jepsen-tests-all.jar' }
}

task copyTimelockServer(type: Copy, dependsOn: ':timelock-server-distribution:distTar') {
    from tasks.getByPath(':timelock-server-distribution:distTar').outputs
    into 'resources/atlasdb/'
    rename { filename -> 'timelock-server.tgz' }
}

task jepsenTest(type: Exec, dependsOn: ['copyShadowJar', 'copyTimelockServer']) {
    // TODO -- When fixing jepsen tests, uncomment and fix this line
    // environment = project.dockerCompose.environment
    commandLine project.dockerCompose.composeCommand('run', '--rm', 'jepsen')
}

check.dependsOn jepsenTest
